apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'co.riiid.gradle'
apply plugin: 'com.github.triplet.play'

def prevVersionName = "0.2.0"
def currVersionName = "0.2.1"
def currVersionCode = 5
def prevTagName = "${prevVersionName}"
def currTagName = "${currVersionName}"
def changelog = """
https://github.com/yeobara/yeobara-android/compare/${prevTagName}...${currTagName}
"""

def props = new Properties()
props.load(new FileInputStream(file('../local.properties')))

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "io.github.yeobara.android"
        minSdkVersion 21
        targetSdkVersion 23
        versionCode currVersionCode
        versionName currVersionName
    }

    signingConfigs {
        release {
            storeFile file(props.getProperty("storeFile", ""))
            storePassword props.getProperty("storePassword", "")
            keyAlias props.getProperty("keyAlias", "")
            keyPassword props.getProperty("keyPassword", "")
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '.SNAPSHOT'
        }

        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE-FIREBASE.txt'
        exclude 'META-INF/NOTICE'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile "org.jetbrains.kotlin:kotlin-android-extensions:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:cardview-v7:23.1.1'
    compile 'com.android.support:design:23.1.1'
    compile 'com.android.support:recyclerview-v7:23.1.1'
    compile 'com.firebase:firebase-client-android:2.4.0'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.tbruyelle.rxpermissions:rxpermissions:0.4.2@aar'
    compile 'io.reactivex:rxandroid:1.0.1'
    compile 'io.reactivex:rxjava:1.0.14'
}

buildscript {
    ext.kotlin_version = '1.0.0-beta-1103'

    repositories {
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'co.riiid:gradle-github-plugin:0.3.1'
        classpath 'com.github.triplet.gradle:play-publisher:1.1.3'
    }
}

github {
    owner = 'yeobara'
    repo = 'yeobara-android'
    tagName = currTagName
    token = props.get('githubToken')
    targetCommitish = 'master'
    name = currTagName
    body = changelog
    prerelease = true
    assets = [
            'app/build/outputs/apk/app-release.apk',
            'app/build/outputs/apk/app-debug.apk',
            'app/build/outputs/mapping/release/mapping.txt',
            'app/build/outputs/',
    ]
}

play {
    serviceAccountEmail = props.get('playStorePublisher')
    pk12File = file(props.get('playStoreP12File'))
    track = 'beta' // 'production' or 'rollout' or 'beta' or 'alpha'
    // userFraction = 0.2 // only necessary for 'rollout', in this case default is 0.1
    uploadImages = true
}
